
plugins {
  id "java"
  id "eclipse"
  id "idea"
  id "com.google.protobuf" version "0.8.4"
  id 'com.github.johnrengelman.shadow' version '2.0.0'
}

def grpcVersion = '1.11.0'
// This version must match the protobuf-java version that grpc transitively pulls in
def protobufVersion = '3.5.1'

repositories {
  mavenCentral()
  mavenLocal()
  maven { url "http://repo.joist.ws" }
}

version = 'git describe --tags --dirty'.execute().text.trim()

dependencies {
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compile "ch.qos.logback:logback-classic:1.2.2"
  compile "org.slf4j:jul-to-slf4j:1.7.25"
  compile 'com.github.jnr:jnr-posix:3.0.43'
  compile 'com.github.jnr:jnr-unixsocket:0.12'
  compile 'org.eclipse.jgit:org.eclipse.jgit:4.2.0.201601211800-r'
  compile 'commons-io:commons-io:2.4'
  compile 'org.apache.commons:commons-lang3:3.4'
  compile 'org.jooq:jool:0.9.10'
  compile 'com.github.rvesse:airline:2.2.0'
  compile 'joist:joist-util:1.13.1'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
  testCompile 'org.mockito:mockito-core:2.13.0'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

jar {
   manifest {
       attributes 'Mirror-Version': version
   }
}

shadowJar {
  version = null
}

eclipse {
  pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
  pathVariables 'M2_REPO': new File("${System.getenv('HOME')}/.m2/repository")
  classpath {
    downloadSources = true
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// E.g. '9.0.1 (Oracle Corporation 9.0.1+11)' or '1.8.0_151 (Oracle Corporation 25.151-b12)'
def currentJvm = org.gradle.internal.jvm.Jvm.current()
def jvmVersion = currentJvm.toString().tokenize('.').take(2).join(".").toDouble()
if (jvmVersion >= 1.9) {
  compileJava {
    options.compilerArgs.addAll(['--release', '8'])
  }
}

