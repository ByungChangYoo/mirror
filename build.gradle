apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.4'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'io.grpc:grpc-netty:0.13.1'
  compile 'io.grpc:grpc-core:0.13.1'
  compile 'io.grpc:grpc-protobuf:0.13.1'
  compile 'io.grpc:grpc-stub:0.13.1'
  compile 'org.slf4j:slf4j-jdk14:1.7.18'
  testCompile 'junit:junit:4.12'
  testCompile 'commons-io:commons-io:2.4'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.0.0-beta-2"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:0.13.1'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

eclipse {
  pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
  classpath {
    file.whenMerged { cp ->
      cp.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/source/proto/main/java/', null))
      cp.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/source/proto/main/grpc/', null))
    }
    downloadSources = true
  }
}
